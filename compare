#!/usr/bin/env groovy
@Grapes(
    @Grab(group='org.bitbucket.mstrobel', module='procyon-compilertools', version='0.5.32')
)

import java.nio.file.Files
import java.nio.file.StandardCopyOption
import java.util.zip.ZipEntry
import java.util.zip.ZipFile
import javax.xml.bind.DatatypeConverter
import java.security.MessageDigest
import com.strobel.decompiler.Decompiler
import com.strobel.decompiler.DecompilerSettings
import com.strobel.decompiler.PlainTextOutput

// Compares 2 zips, 2 directories, a directory and a jar or 2 directories of zips

def cli = new CliBuilder(
    usage: 'compare [options] <oldFile> <newFile>',
    header: 'Options:')
cli._(longOpt: 'help', 'print this message')
cli._(longOpt: 'color', 'use ANSI color sequences to distinguish comparisons')
cli.e(longOpt: 'expand', 'expand archives in the base directory to compare')
cli._(longOpt: 'diff', 'compare files line by line')
cli.h(longOpt: 'hash', 'perform hash comparison')
cli.n(longOpt: 'normalize', args: 1, argName: 'list', 'comma separated filename pattern to normalize')
cli.d(longOpt: 'decompile', 'decompile class files')

options = cli.parse(args)

if (!options || options?.help || options?.arguments()?.size() != 2 ) {
  cli.usage()
  return
}

excludes = ['pom.properties']
normalizePatterns = options.n ? options.n.split(',') : false
settings = DecompilerSettings.javaDefaults()

oldFile = new File(options.arguments()[0])
newFile = new File(options.arguments()[1])

if (oldFile.directory && newFile.directory && options.e) {
  oldFile.listFiles().each { File file ->
    println "Comparing ${file.name}..."

    File subLeft = file
    File subRight = new File(newFile, subLeft.name)

    if(!subRight.exists()){
      println 'Missing on the new file!'
      println '\n'
      return
    }

    compare(collect(subLeft), collect(subRight) )
    println '\n'
  }
} else {
  compare(collect(oldFile), collect(newFile))
}

void printMiss(message) {
  if (options.color) {
    println("\033[0;31m$message\033[0m")
  } else {
    println(message)
  }
}

void printAdd(message) {
  if (options.color) {
    println("\033[0;32m$message\033[0m")
  } else {
    println(message)
  }
}

void printDiff(message) {
  if (options.color) {
    println("\033[0;36m$message\033[0m")
  } else {
    println(message)
  }
}

void diff(InputStream left, InputStream right = null, decompile = false) {
  File a, b
  if (options.diff && left) {
    File tmp1 = File.createTempFile('left', decompile ? '.class' : null)
    tmp1.deleteOnExit()
    Files.copy(left, tmp1.toPath(), StandardCopyOption.REPLACE_EXISTING)
    a = tmp1
    if (decompile && options.decompile) a = decompileClass tmp1
    if (right) {
      File tmp2 = File.createTempFile('right', decompile ? '.class' : null)
      tmp2.deleteOnExit()
      Files.copy(right, tmp2.toPath(), StandardCopyOption.REPLACE_EXISTING)
      b = tmp2
      if (decompile && options.decompile) b = decompileClass tmp2
      println('='*80)
      execute("diff -ytd --suppress-common-lines --strip-trailing-cr $a $b")
    } else {
      execute("cat $a")
    }
  }
}

File decompileClass(File file) {
  File src = File.createTempFile('decompiled', null)
  src.deleteOnExit()
  src.withWriter { Writer writer ->
    Decompiler.decompile(file.canonicalPath, new PlainTextOutput(writer), settings)
  }
  src
}

void execute(cmd) {
  def stdout = new StringBuilder(), stderr = new StringBuilder()
  def proc = "$cmd".execute()
  proc.consumeProcessOutput(stdout, stderr)
  proc.waitFor()
  println stdout
}

void compare(Map<String, Tuple2> leftData, Map<String, Tuple2> rightData){
  boolean different = false

  leftData.each { k, v ->
    if(!rightData[k]){
      different = true
      printMiss "-- ${k}"
//      if (v.second != 'd') {
//        diff(v.first, null, k.endsWith('.class'))
//      }
    }
    else if(excludes.every { !k.endsWith(it) } && rightData[k].second != v.second ){
      different = true
      printDiff "!= ${k}"
      if (v.second != 'd') {
        diff(v.first, rightData[k].first, k.endsWith('.class'))
      }
    }
  }

  rightData.each { k, v ->
    if(!leftData[k]){
      different = true
      printAdd "++ ${k}"
//      if (v.second != 'd') {
//        diff(v.first, null, k.endsWith('.class'))
//      }
    }
  }

  if(!different){
    println "No differences."
  }
}

String normalize(String key){
  if(options.n){
    String nkey = key
    normalizePatterns.each { String pattern ->
      nkey = nkey.replaceAll(pattern, '')
    }
    return nkey
  }
  key
}

Map<String, Tuple2> collect(File root){
  Map data = [:]
  String key

  if(root.directory){
    root.eachFileRecurse { File subfile ->
      key = normalize(subfile.canonicalPath - (root.canonicalPath + '/'))

      Tuple2 pair = new Tuple2(
          subfile.directory ? null : subfile.newInputStream(),
          subfile.directory ? 'd' : collectData(subfile.newInputStream()) 
      )
      data << [(key): pair]
    }
  }
  else if(['.jar', '.zip', '.kar'].any{ root.name.endsWith(it) }){
    // zip files
    ZipFile zip = new ZipFile(root)
    zip.entries().each { ZipEntry entry ->
      key = normalize(entry.directory ? entry.name[0..-2] : entry.name)

      Tuple2 pair = new Tuple2(
          zip.getInputStream(entry),
          entry.directory ? 'd' : collectData(zip.getInputStream(entry))
      )

      data << [(key): pair]
    }
  }
  else {
    // whatever file
    key = normalize(root.canonicalPath)

    Tuple2 pair = new Tuple2(
        root.newInputStream(),
        collectData(root.newInputStream()))

    data << [(key): pair]
  }

  data
}

String collectData(InputStream stream) {
  if(!options.h){
    return '0'
  }

  MessageDigest md = MessageDigest.getInstance("SHA")
  md.update(stream.bytes)
  stream.close()

  DatatypeConverter.printHexBinary(md.digest())
}